group 'ru.spbau.mit'
version '1.0'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'antlr'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    generated {
        java {
            srcDir "generated-src/antlr/main"
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    generatedCompile "org.antlr:antlr4-runtime:4.5.1"
    antlr "org.antlr:antlr4:4.5.1"
    compile sourceSets.generated.output

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'ru.spbau.mit']
    arguments += ['-visitor']
    outputDirectory = new File("generated-src/antlr/main/ru/spbau/mit".toString())
}

compileKotlin.dependsOn generateGrammarSource

clean{
    delete "generated-src"
}

idea {
    module {
        sourceDirs += file("generated-src/antlr/main")
    }
}

compileKotlin.source sourceSets.generated.java, sourceSets.main.java
